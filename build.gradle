buildscript {
    dependencies {
        classpath 'de.guntram.mcmod:crowdin-translate:1.3+1.17'
    }
    repositories {
        maven {
            name = 'CrowdinTranslate source'
            url = "https://minecraft.guntram.de/maven/"
        }
    }
}

plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
}

apply plugin: 'de.guntram.mcmod.crowdin-translate'
crowdintranslate.crowdinProjectName = 'cleardespawn'

repositories {
    maven { url = "https://maven.fabricmc.net/" }
    maven { url = "https://minecraft.guntram.de/maven/" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = "cleardespawn"
ext.projectVersion = '1.1.1'
version = "${project.minecraft_version}-${project.mod_version}"

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modImplementation "de.guntram.mcmod:crowdin-translate:${project.crowdintranslate_version}"
    include "de.guntram.mcmod:crowdin-translate:${project.crowdintranslate_version}"
    modImplementation "de.guntram.mcmod:GBfabrictools:${project.gbfabrictools_version}"
    include "de.guntram.mcmod:GBfabrictools:${project.gbfabrictools_version}"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

build {
    dependsOn downloadTranslations
}